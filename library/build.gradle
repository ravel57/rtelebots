plugins {
	id 'java-library'
	id 'maven-publish'
	id 'jacoco'
	id 'org.jetbrains.kotlin.jvm' version '1.9.22'
}

group = "com.github.ravel57"
version = "9.0.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	api 'com.google.code.gson:gson:2.10.1'
	api 'com.squareup.okhttp3:okhttp:4.12.0'
	api 'com.squareup.okhttp3:logging-interceptor:4.12.0'
	api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
	api "org.jetbrains.kotlin:kotlin-reflect:1.9.22"
	api 'org.jetbrains:annotations:24.1.0'

	testImplementation 'junit:junit:4.13.1'
	testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.1.13'
	testImplementation 'org.reflections:reflections:0.9.12'
	testImplementation 'org.jetbrains.kotlin:kotlin-reflect'
}

jar {
	manifest {
		attributes "Automatic-Module-Name": "telegram.bot.api"
	}
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

javadoc {
	options.encoding = 'UTF-8'
}

test {
	if (project.hasProperty('excludeTests')) {
		exclude project.property('excludeTests')
	}
	if (project.hasProperty('prTest')) {
		// TODO move "functional" tests into specific dir and exclude it
		exclude '**/Base64Test.class'
		exclude '**/CheckTelegramAuthTest.class'
		exclude '**/PaymentsTest.class'
		exclude '**/TelegramBotTest.class'
		exclude '**/UpdatesListenerTest.class'
	}
}

tasks.withType(Test).configureEach {
	testLogging {
		events "started", "passed", "skipped", "failed"
	}
}

apply from: rootProject.file('gradle/gradle-publish-plugin.gradle')

tasks.withType(Test) {
	enabled = false
}

archivesBaseName = "rtelebots"